# # # # # # # # # # # # # # #
# TCSH Grammar              #
# # # # # # # # # # # # # # #

###### Grammar ######
program             ::=     [ block ] EOF
                    ;

block               ::=     statement [ block ]
                    ;

statement           ::=     subshell | compound_command | control
                    ;

compound_command    ::=     [ grouping ] separator
                    ;

subshell            ::=     LPARANTH block RPARANTH
                    ;

grouping            ::=     pipeline ( ( AND_IF | OR_IF ) grouping )*
                    ;

pipeline            ::=     command ( PIPE pipeline )*
                    ;

command             ::=     ( WORD | redirection ) [ command ]
                    ;

redirection         ::=     [ IO_NUMBER ] GREAT WORD
                    |       [ IO_NUMBER ] DGREAT WORD
                    |       LESS WORD
                    |       DLESS WORD
                    ;

separator           ::=     NEWLINE | SEMI | EOF
                    ;

# Control Flow !
control             ::=     if_control | while_control | foreach_control | repeat_control
                    ;

if_control          ::=     IF conditional THEN NEWLINE [ block ] [ else_if_control ] ENDIF NEWLINE
                    ;

else_if_control     ::=     ELSE_IF conditional THEN NEWLINE [ block ] [ else_control ]
                    ;

else_control        ::=     ELSE NEWLINE [ block ]
                    ;

foreach_control     ::=     FOREACH WORD wordlist_expression [ block ] END NEWLINE
                    ;

while_control       ::=     WHILE conditional THEN NEWLINE [ block ] END NEWLINE
                    ;

repeat_control      ::=     REPEAT WORD grouping
                    ;

wordlist_expression ::=     LPARANTH wordlist RPARANTH
                    ;

wordlist            ::=     WORD [ wordlist ]
                    ;

conditional         ::=     LPARANTH WORD RPARANTH
                    ;
# ! Control Flow
