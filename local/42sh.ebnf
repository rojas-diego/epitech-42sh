# # # # # # # # # # # # # # #
# TCSH Grammar              #
# # # # # # # # # # # # # # #

###### Grammar ######
program             ::=     [ block ] EOF
                    ;

block               ::=     statement [ block ]
                    ;

statement           ::=     subshell | compound_command | control
                    ;

compound_command    ::=     [ grouping ] separator
                    ;

subshell            ::=     OPARANTH block CPARANTH
                    ;

grouping            ::=     pipeline ( ( AND_IF | OR_IF ) grouping )*
                    ;

pipeline            ::=     command ( PIPE pipeline )*
                    ;

command             ::=     WORD [ redirection ] [ command ]
                    ;

redirection         ::=     [ IO_NUMBER ] GREAT WORD
                    |       [ IO_NUMBER ] DGREAT WORD
                    |       LESS WORD
                    |       DLESS WORD
                    ;

separator           ::=     NEWLINE | SEMI | EOF
                    ;

# Control Flow !
control             ::=     if_control | while_control | foreach_control | repeat_control
                    ;

if_control          ::=     IF conditional THEN NEWLINE [ block ] [ else_if_control ] ENDIF NEWLINE
                    ;

else_if_control     ::=     ELSE_IF conditional NEWLINE [ block ] [ else_control ]
                    ;

else_control        ::=     ELSE NEWLINE [ block ]
                    ;

foreach_control     ::=     FOREACH NAME wordlist_expression
                    ;

while_control       ::=     WHILE conditional NEWLINE block END NEWLINE
                    ;

repeat_control      ::=     REPEAT WORD grouping
                    ;

wordlist_expression ::=     OPARANTH wordlist CPARANTH
                    ;

wordlist            ::=     WORD [ wordlist ]
                    ;

conditional         ::=     OPARANTH WORD CPARANTH
                    ;
# ! Control Flow
